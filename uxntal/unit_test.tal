( unit_test.tal )
( devices )
|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console &vector $2 &read $1 &pad $5 &write $1 &error $1

|50 @cell $2

( main program )
|0100
@on-reset
    ( hi with DEO )
    #68 .Console/write DEO
    #69 .Console/write DEO
    #0a .Console/write DEO

    ( ADD, EQU )
    #01 #02 ADD
    #03 EQU
    #40 ADD
    .Console/write DEO ( A )

    ( SUB, DUP, NEQ )
    #10 DUP #03 SUB
    NEQ
    #40 ADD
    .Console/write DEO ( A )

    ( MUL, GTH )
    #10 DUP #02 MUL
    GTH
    #40 ADD
    .Console/write DEO ( @ )

    ( end line )
    #0a .Console/write DEO

    ( JCN, SWP, POP )
    #7071 #01 ,&pass JCN SWP &pass POP ( p )
    .Console/write DEO

    #7071 #00 ,&fail JCN SWP &fail POP ( q )
    .Console/write DEO

    ( end line )
    #0a .Console/write DEO

    ( LDZ, STZ )
    .cell LDZ
    #40 ADD
    DUP .Console/write DEO ( @ )
    #30 ADD
    .cell STZ
    .cell LDZ
    .Console/write DEO ( p )

    ( end line )
    #0a .Console/write DEO

    ( ADD2, LTH2/GTH2 )
    #0020 DUP2
    #0400 ADD2
    GTH2 ( true )
    #42 ADD
    .Console/write DEO ( B )

    ( end line )
    #0a .Console/write DEO

    ( JCI )
    #03
    &loop
        #61 .Console/write DEO
        #01 SUB DUP ?&loop
    POP
    #0a .Console/write DEO  ( aaa )

    ( terminate )
    #80 .System/state DEO
BRK
